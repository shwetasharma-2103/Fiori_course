@Metadata.layer: #CORE
@UI.headerInfo: { typeName: 'Travel',
                  typeNamePlural: 'Travels',
                  title: { type: #STANDARD, value: 'TravelID' },
                  description: { type: #STANDARD, value: 'Description' }  }
annotate view ZC_FE_TRAVEL_000288 with
{

  @UI.facet: [{ purpose: #HEADER,
                position: 10,
                type: #DATAPOINT_REFERENCE,     //For a single field
                targetQualifier: 'PriceData' },
              {
                purpose: #HEADER,
                position: 20,
                type: #DATAPOINT_REFERENCE,
                targetQualifier: 'StatusData'
              },
//              For General details - Collection of facets
              {
                label: 'General Information',
                id: 'GeneralInfo',
                position: 10,
                type: #COLLECTION
              },
//              Field Group - 'General' which contains all identification fields
              {
                label: 'General',
                purpose: #STANDARD,
                position: 10,
                type: #IDENTIFICATION_REFERENCE,
                parentId: 'GeneralInfo' 
              },  
//              Field Group - 'Dates' 
              {
                label: 'Dates',
                purpose: #STANDARD,
                position: 20,
                type: #FIELDGROUP_REFERENCE,
                parentId: 'GeneralInfo' ,
                targetQualifier: 'Dates'
              },
 //              Field Group - 'Prices' 
              {
                label: 'Prices',
                purpose: #STANDARD,
                position: 30,
                type: #FIELDGROUP_REFERENCE,
                parentId: 'GeneralInfo',
                targetQualifier: 'Prices' 
              }  ,
              
              {
                label: 'Bookings',
                purpose: #STANDARD,
                type: #LINEITEM_REFERENCE,
                position: 20,
                targetElement: '_Booking' 
              }
              
                          
              ]

  @UI.lineItem: [{ position: 10 }]
  TravelID;
  @UI.identification: [{ position: 10 }]
  Description;
  @UI.lineItem: [{ position: 20 }]
  @UI.selectionField: [{ position: 10 }]
  @UI.identification: [{ position: 20 }]
  AgencyID;
  @UI.lineItem: [{ position: 30 }]
  @UI.selectionField: [{ position: 20 }]
  @UI.identification: [{ position: 30 }]
  CustomerID;
  @UI.lineItem: [{ position: 40 }]
  @UI.fieldGroup: [{ position: 10, qualifier: 'Dates'  }]
  BeginDate;
  @UI.lineItem: [{ position: 50 }]
  @UI.fieldGroup: [{ position: 20, qualifier: 'Dates'  }]
  EndDate;
  @UI.lineItem: [{ position: 60 }]
  @UI.fieldGroup: [{ position: 10, qualifier: 'Prices'  }]
  BookingFee;
  @UI.lineItem: [{ position: 70 }]
  @UI.dataPoint: { qualifier: 'PriceData', title: 'Total Price' }
  TotalPrice;
  @UI.lineItem: [{ position: 80, criticality: 'OverallStatusCriticality' }]
  @UI.selectionField: [{ position: 20 }]
  @UI.textArrangement: #TEXT_ONLY
  @UI.dataPoint: { qualifier: 'StatusData', title: 'Status', criticality: 'OverallStatusCriticality' }
  OverallStatus;
  @UI.lineItem: [{ position: 90 }]
  LastChangedAt;


}